---
layout: post
title:  "텍스트 카피해서 그리드에 붙여넣기"
description: 텍스트 카피해서 그리드에 붙여넣기
date:   2012-07-04 12:38:44 +0530
categories: Flex
---

꼼수로서 잠시 clipboard에 복사해놓고 가져와 파싱해서 붙여넣기를 하는것이다.

```flex
/**
                 * Method Name : makeExcelDataGrid
                 * Desc : 액셀 데이타를 데이타그리드에 붙여넣기위한 선행작업을 한다.
                 * return void
                 */
                private function makeExcelDataGrid(event:KeyboardEvent):void
                {
                        FI_FcommonUtil.readyForPasteExcelData(event, viewMD);
                }


///////////////////////////////////////////////////////////////////////////////////////////////////

/**
                 * Desc : 이벤트가 일어난 FI_DataGrid에 액셀데이타를 붙여넣을 수 있도록 준비작업을 한다.
                 * Method Name : readyForPasteExcelData
                 * return void
                 */
                public static function readyForPasteExcelData(event:KeyboardEvent, view:Module):void
                {
                        var dgObj:FI_DataGrid = event.currentTarget as FI_DataGrid;
                        
                        if(event.ctrlKey && !dgObj.getChildByName("clipboardProxy")){
                                var txtField:TextField = new TextField();
                                txtField.name = "clipboardProxy";
                                txtField.visible = false;
                                txtField.multiline = true;
                                txtField.type = TextFieldType.INPUT;
                                txtField.addEventListener(TextEvent.TEXT_INPUT, pasteDataFromClipboard);
                                dgObj.addChild(txtField);
                                txtField.setSelection(0, txtField.text.length-1);
                                view.systemManager.stage.focus = txtField;
                        }
                }
                
                /**
                 * Desc : 클립보드로(TextField) 부터 데이타를 가져와 해당 그리드에 붙여넣는다.
                 * return void
                 */
                public static function pasteDataFromClipboard(event:TextEvent):void
                {
                        var dgObj:FI_DataGrid = (event.currentTarget as TextField).parent as FI_DataGrid;
                        var parsedData:Array = parseExcelData(event.text, dgObj.columns);
                        var dgAc:ArrayCollection = dgObj.dataProvider as ArrayCollection;
                        
                        for each(var item:Object in parsedData){
                                dgAc.addItem(item);
                        }
                        
                        var txtField:TextField = dgObj.getChildByName("clipboardProxy") as TextField;
                        if(txtField){
                                dgObj.removeChild(txtField);
                        }
                }
                
                /**
                 * Desc : 액셀데이타를 Array 형태로 파싱한다.
                 * Method Name : parseExcelData
                 * param txtVal 액셀 데이타
                 * param columns 그리드의 컬럼들
                 * return Array 파싱된 데이타
                 */
                public static function parseExcelData(txtVal:String, columns:Array):Array
                {
                        var rowArr:Array = txtVal.split("\n");
                        if(!rowArr[rowArr.length-1]) rowArr.pop();
                        
                        var rstArr:Array = [];
                        
                        for each(var row:String in rowArr){
                                var fieldArr:Array = row.split("\t");
                                var clmnLength:uint = columns.length;
                                var fieldLength:uint = fieldArr.length;
                                var initClmn:uint = 0;
                                
                                if(columns[0].dataField == "select") initClmn++;
                                
                                var item:Object = {};
                                for(var i:uint=initClmn,j:uint=0; i<clmnLength; i++,j++){
                                        item[columns[i].dataField] = j<fieldLength?fieldArr[j]:"";
                                }
                                rstArr.push(item);
                        }
                        return rstArr;
                }
```
