---
layout: post
title:  "Flex 스크롤 안생기게..."
description: Flex 스크롤 안생기게 하기
date:   2012-07-04 12:35:44 +0530
categories: Oracle Sql
---

* 반드시 보여주는 컨텐츠안의 이미지및, swf는 가변형이어야 한다.

```mxml
public class FI_IProcessMap extends Box
        {
                //===================================================
                //
                //        Variables
                //
                //===================================================
                
                /**
                 * MapObject Process Map
                 */
                public static const MAP_OBJECT:String = "mapObject";
                
                /**
                 * Arrow Process Map
                 */
                public static const ARROW:String = "arrow";
                
                /**
                 * 변경 MapObject 좌표 
                 */
                public static const MAP_OBJECT_COORDINATE:String = "mapObjectCoordinate";
                
                /**
                 * 변경 화살표 좌표 
                 */
                public static const ARROW_COORDINATE:String = "arrowCoordinate";
                
                /**
                 * 변경 MapObject 정보 
                 */
                public static const MAP_OBJECT_INFO:String = "mapObjectInfo";
                
                /**
                 * 변경 화살표 정보 
                 */
                public static const ARROW_INFO:String = "arrowInfo";
                
                /**
                 * Canvas 객체
                 */
                private var canvas:Canvas;
                
                /**
                 * MapObject 좌표
                 */
                private var _mapObjectCoordinate:ArrayCollection;
                
                /**
                 * 화살표 좌표
                 */
                private var _arrowCoordinate:ArrayCollection;
                
                /**
                 * Process Map 정보
                 */
                private var _processMapInfo:Object;
                
                /**
                 * Process Map 종류
                 */
                private var _processMapKind:String;
                
                /**
                 * 화살표 정보
                 */
                private var arrowInfo:ArrayCollection;
                
                /**
                 * MapObject 정보
                 */
                private var mapObjectInfo:ArrayCollection;
                
                /**
                 * MapObject 객체
                 */
                private var mapObject:FI_IMapObject;
                
                /**
                 * 화살표 객체
                 */
                private var arrow:FI_IArrow;
                
                
                /**
                 * Constructor
                 */
                public function FI_IProcessMap()
                {
                        super();
                        super.percentHeight = super.percentWidth = 100;
                        super.horizontalScrollPolicy = super.verticalScrollPolicy = ScrollPolicy.OFF;
                        
                        canvas = new Canvas();
                        canvas.percentHeight = canvas.percentWidth = 100;
                        canvas.horizontalScrollPolicy = canvas.verticalScrollPolicy = ScrollPolicy.OFF;
                }
                
                
                //===========================================
                //
                //        Overriden Methods
                //
                //===========================================
                
                /**
                 * 
                 */
                override protected function createChildren():void{
                        super.createChildren();
                        super.addChild(canvas);
                        
                        parent.addEventListener(ResizeEvent.RESIZE, resizeHandler);
                }
                
                /**
                 * 
                 */
                override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void{
                        super.updateDisplayList(unscaledWidth, unscaledHeight);
                        
                        if(parent != null){
                                var boxWidth:Number = super.width;
                                var boxHeight:Number = super.height;
                                var mdWidth:Number = parent.width;
                                var mdHeight:Number = parent.height;
                                
                                if(boxWidth != 0 && boxHeight != 0 && mdWidth != 0 && mdHeight != 0){
                                        super.scaleX = mdWidth / boxWidth;
                                        super.scaleY = mdHeight / boxHeight;
                                }
                        }
                }
                
                
                //===========================================
                //
                //        Event Handlers
                //
                //===========================================
                
                /**
                 * size가 변경되었을때 발생하는 이벤트 핸들러
                 */
                private function resizeHandler(event:ResizeEvent):void{
                        super.invalidateDisplayList();
                }
                
                /**
                 * MapObject를 클릭했을때 발생하는 이벤트 핸들러
                 */
                private function mapObjectClickHandler(event:FI_IMapObjectEvent):void{
                        super.dispatchEvent(event);        //redispatch 시킨다.
                }
                
                
                //===========================================
                //
                //        Setters & Getters
                //
                //===========================================
                
                public function set mapObjectCoordinate(value:ArrayCollection):void{
                        _mapObjectCoordinate = ObjectUtil.copy(value) as ArrayCollection;
                }
                
                public function set arrowCoordinate(value:ArrayCollection):void{
                        _arrowCoordinate = ObjectUtil.copy(value) as ArrayCollection;
                }
                
                public function set processMapKind(value:String):void{
                        _processMapKind = value;
                }
                
                public function set processMapInfo(value:Object):void{
                        if(value){
                                _processMapInfo = ObjectUtil.copy(value);
                                
                                arrowInfo = _processMapInfo.arrowInfo;        //화살표 정보 저장
                                mapObjectInfo = _processMapInfo.mapObjectInfo;        //MapObject 정보 저장
                                
                                createProcessMap();
                        }
                }
                
                
                //===========================================
                //
                //        Etc Functions
                //
                //===========================================
                
                /**
                 * ProcessMap을 생성한다.(생성순서:default화살표->progress화살표->MapObject)
                 */
                private function createProcessMap():void{
                        canvas.removeAllChildren();
                        
                        var progressAc:ArrayCollection = new ArrayCollection();
                        
                        //default 화살표 생성
                        for each(var defaultArrowObj:Object in arrowInfo){
                                if(defaultArrowObj.swfStatus == "progress"){
                                        progressAc.addItem(defaultArrowObj);
                                        continue;
                                }
                                createArrow(defaultArrowObj);
                        }
                        
                        //progress 화살표 생성
                        for each(var progressArrowObj:Object in progressAc){
                                createArrow(progressArrowObj);
                        }
                        
                        //MapObject 생성
                        for each(var mapObjectObj:Object in mapObjectInfo){
                                createMapObject(mapObjectObj);
                        }
                }
                
                /**
                 * 화살표를 만든다.
                 * 
                 * @param arrowInfoObj 화살표 정보를 가지고 있는 객체
                 */
                private function createArrow(arrowInfoObj:Object):void{
                        var coordinateObj:Object = FI_ICommonUtil.findItemInArrayCollection("arrowObj", arrowInfoObj.arrowObj, _arrowCoordinate);
                        
                        arrow = new FI_IArrow();
                        canvas.addChild(arrow);
                        
                        arrow.id = arrowInfoObj.arrowObj;
                        arrow.x = coordinateObj.x;
                        arrow.y = coordinateObj.y;
                        arrow.source = CM_propertiesManager.getInstance().imagePath + "fi/swf/FI_" + _processMapKind + "Arrow" + arrowInfoObj.arrowObj + "_" + arrowInfoObj.swfStatus + ".swf";
                }
                
                /**
                 * MapObject를 만든다.
                 * 
                 * @param mapInfoObj MapObject 정보를 가지고 있는 객체
                 */
                private function createMapObject(mapInfoObj:Object):void{
                        var coordinateObj:Object = FI_ICommonUtil.findItemInArrayCollection("mapObj", mapInfoObj.mapObj, _mapObjectCoordinate);
                        
                        mapObject = new FI_IMapObject();
                        canvas.addChild(mapObject);
                        
                        mapObject.id = mapInfoObj.mapObj;
                        mapObject.x = coordinateObj.x;
                        mapObject.y = coordinateObj.y;
                        mapObject.mapInfo = mapInfoObj;
                        
                        //action이 있는것만 이벤트 등록
                        if(mapInfoObj.isAction){
                                addMapObjectClickEvent(mapObject);
                        }
                }
                
                /**
                 * 속성값들을 변경한다.
                 * 
                 * @param changeObjectKind 변경 객체 종류
                 * @param objId 객체 이름
                 * @param changeFields 변경 필드명들
                 * @param changeValues 변경 데이타값들
                 */
                public function changeProperties(changeObjectKind:String, objId:String, changeFields:Array, changeValues:Array):void{
                        if(changeFields != null && changeValues != null && changeFields.length != changeValues.length) throw new Error("변경 필드와 변경값의 개수가 일치하여야 합니다.");
                        
                        var changeLength:int = changeFields.length;
                        var changeMapObjct:FI_IMapObject;
                        var changeArrow:FI_IArrow;
                        var changeAc:ArrayCollection;
                        
                        if(changeObjectKind == MAP_OBJECT_INFO || changeObjectKind == MAP_OBJECT_COORDINATE){        //MapObject
                                if(changeObjectKind == MAP_OBJECT_INFO){        //정보
                                        changeAc = mapObjectInfo;
                                }else if(changeObjectKind == MAP_OBJECT_COORDINATE){        //좌표
                                        changeAc = _mapObjectCoordinate;
                                }
                                
                                for each(var mapObjectItem:Object in changeAc){
                                        if(mapObjectItem.mapObj == objId){
                                                changeMapObjct = findObject(MAP_OBJECT, objId) as FI_IMapObject;
                                                
                                                for(var i:int=0; i<changeLength; i++){
                                                        mapObjectItem[changeFields[i]] = changeValues[i];        //데이타 값 변경
                                                        changeMapObjct[changeFields[i]] = changeValues[i];        //화면 변경
                                                        
                                                        //properties가 isAction 이고 true면 이벤트 등록
                                                        if(changeFields[i] == "isAction" && changeValues[i]){
                                                                addMapObjectClickEvent(changeMapObjct);
                                                        }
                                                }
                                        }
                                }
                        }else if(changeObjectKind == ARROW_INFO || changeObjectKind == ARROW_COORDINATE){        //화살표
                                if(changeObjectKind == ARROW_INFO){        //정보
                                        changeAc = arrowInfo;
                                }else if(changeObjectKind == ARROW_COORDINATE){        //좌표
                                        changeAc = _arrowCoordinate;
                                }
                                
                                for each(var arrowItem:Object in changeAc){
                                        if(arrowItem.arrowObj == objId){
                                                changeArrow = findObject(ARROW, objId) as FI_IArrow;
                                                
                                                for(var j:int=0; j<changeLength; j++){
                                                        arrowItem[changeFields[j]] = changeValues[j];        //데이타 값 변경
                                                        changeArrow[changeFields[j]] = changeValues[j];        //화면 변경
                                                }
                                        }
                                }
                        }
                }
                
                /**
                 * 객체들을 삭제한다.
                 * 
                 * @removeKind 삭제할 객체 종류
                 * @removeObjects 삭제할 객체 이름들
                 */
                public function removeObjects(removeKind:String, removeObjects:Array):void{
                        var removeAc:ArrayCollection;
                        var removeField:String;
                        
                        if(removeKind == MAP_OBJECT){        //MapObject
                                removeAc = mapObjectInfo;
                                removeField = "mapObj";
                        }else if(removeKind == ARROW){        //화살표
                                removeAc = arrowInfo;
                                removeField = "arrowObj";
                        }
                        
                        var rmvLength:int = removeAc.length-1;
                        var rmvObj:Object;
                        
                        for(var i:int=rmvLength; i>=0; i--){
                                rmvObj = removeAc.getItemAt(i);
                                
                                if(removeObjects.indexOf(rmvObj[removeField]) != -1){
                                        removeAc.removeItemAt(i);        //데이타 삭제
                                        canvas.removeChildAt(findObjectIndex(removeKind, rmvObj[removeField]));        //화면 제거
                                }
                        }
                }
                
                /**
                 * 객체를 추가한다.
                 * 
                 * @param addObjectKind 객체 추가 종류
                 * @param addDatas 추가할 데이타
                 */
                public function addObjects(addObjectKind:String, addDatas:Array):void{
                        for each(var addObj:Object in addDatas){
                                //MapObject
                                if(addObjectKind == MAP_OBJECT){        
                                        mapObjectInfo.addItem(addObj);        //데이타 추가
                                        createMapObject(addObj);        //화면 추가
                                        
                                //화살표
                                }else if(addObjectKind == ARROW){        
                                        arrowInfo.addItem(addObj);        //데이타 추가
                                        createArrow(addObj);        //화면 추가
                                }
                        }
                }
                
                /**
                 * 해당 객체를 찾아준다.
                 * 
                 * @param kind 객체 종류[M:MapObject | A:Arrow]
                 * @param objId 찾을 객체 id
                 */
                private function findObject(kind:String, objId:String):UIComponent{
                        for each(var ui:UIComponent in canvas.getChildren()){
                                if(kind == MAP_OBJECT && ui is FI_IArrow){
                                        continue;
                                }else if(kind == ARROW && ui is FI_IMapObject){
                                        continue;
                                }
                                
                                if(ui.id == objId){
                                        return ui;
                                }
                        }
                        return null;
                }
                
                /**
                 * 해당 객체의 index를 찾아준다.
                 * 
                 * @param kind 객체 종류[M:MapObject | A:Arrow]
                 * @param objId 찾을 객체 id
                 */
                private function findObjectIndex(kind:String, objId:String):int{
                        var childrenLength:int = canvas.getChildren().length;
                        var ui:UIComponent;
                        var idx:int;
                        
                        for(var i:int=0; i<childrenLength; i++){
                                ui = canvas.getChildAt(i) as UIComponent;
                                
                                if(kind == MAP_OBJECT && ui is FI_IArrow){
                                        continue;
                                }else if(kind == ARROW && ui is FI_IMapObject){
                                        continue;
                                }
                                
                                if(ui.id == objId){
                                        idx = i;
                                }
                        }
                        return idx;
                }
                
                /**
                 * FI_IMapObjectEvent.MAP_OBJECT_CLICK 이벤트를 등록해준다.
                 * 
                 * @param addMapObject 이벤트를 등록할 MapObject 객체
                 */
                private function addMapObjectClickEvent(addMapObject:FI_IMapObject):void{
                        addMapObject.addEventListener(FI_IMapObjectEvent.MAP_OBJECT_CLICK, mapObjectClickHandler);
                }
                
                
        }
```
